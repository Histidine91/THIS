piece base, foot, turret, barrel, cannon, gp0, pflak, pbarrel, pf0, pf1;

#include "THIS.h"

static-var pf;

Create() {
	fireStealthTime = 1500;
	if(!get PERK_MORE_GUNS) {
		hide pflak;
		hide pbarrel;
	}
	if (get PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-1,KHEAVY_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-1,KHEAVY_ROF_BOOST);
	}
}

NewPerk(p) {
	if(p==PERK_MORE_GUNS) {
		show pflak;
		show pbarrel;
	}
	if (p== PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-1,KHEAVY_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-1,KHEAVY_ROF_BOOST);
	}
}

Killed() {
	emit-sfx 1025 from base;
	sleep 600;
	hide foot;
	hide turret;
	hide barrel;
	hide cannon;
	hide pflak;
	hide pbarrel;
}

#define MUZZLE (1026 + get PERK_BETTER_KINETICS)

AimFromWeapon1(p) {
	p=turret;
}

QueryWeapon1(p) {
	p=gp0;
}

AimWeapon1(h,p) {
	signal SIG_Aim1;
	set-signal-mask SIG_Aim1;
	turn turret to y-axis h speed <40>;
	turn barrel to x-axis 0 - p speed <20>;
	wait-for-turn turret around y-axis;
	wait-for-turn barrel around x-axis;
	return 1;
}

Shot1() {
	set STEALTH to 0;
	start-script RestoreStealth();
	emit-sfx MUZZLE from gp0;
	move cannon to z-axis [-20] speed [100];
	sleep 300;
	move cannon to z-axis [0] speed [10];
}


AimFromWeapon2(p) {
	p=pflak;
}

QueryWeapon2(p) {
	p=pf0+pf;
}

AimWeapon2(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn pflak to y-axis h speed <270>;
	turn pbarrel to x-axis 0 - p speed <200>;
	wait-for-turn pflak around y-axis;
	wait-for-turn pbarrel around x-axis;
	return 1;
}

Shot2() {
	pf = !pf;
}