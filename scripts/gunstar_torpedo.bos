//linear constat = 65536

piece body, thrusters, wingl, wingr, damage0, damage1;
piece torpl1, torpl2, torpr1, torpr2, torpl1flare, torpl2flare, torpr1flare, torpr2flare;
piece turret, sleeve, barrell, barrelr, flarel, flarer;
#include "THIS.h"

static-var gun1;

DamageLoop() {
	signal SIG_Damage;
	set-signal-mask SIG_Damage;
	while((get HEALTH) < 50) {
		set CEG_DAMAGE to 25 - (get HEALTH)/2;
		emit-sfx 1026 from damage0;
		if ((get HEALTH) < 30) emit-sfx 1026 from damage1;
		sleep 50;
	}
}

HitByWeaponId(z,x,id,d) {
	start-script DamageLoop();
	return 100;
}

Create() {
	if (!get PERK_MORE_GUNS) {
		hide turret;
		hide sleeve;
		hide barrell;
		hide barrelr;
	}
	gun1 = 0;
	permaStealth = 1;
	
	sleep 30; //to let the MoveCtrl move it
	//emit-sfx 1025 from body;
}

Killed() {
	emit-sfx 1024 from body;
	//get PLAY_SOUND(S_DEATH_SMALL,[5]);
	sleep 100;
	hide body;
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		hide turret;
		hide sleeve;
		hide barrell;
		hide barrelr;
	}
}

#define TURRETAIM <160>
#define BARRELAIM <120>

AimFromWeapon2(p) {
	p=turret;
}

QueryWeapon2(p) {
	if (gun1) {p=flarel;}
	else {p=flarer;}
}

AimWeapon2(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn turret to y-axis h speed TURRETAIM;
	turn sleeve to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	return 1;
}

AimFromWeapon1(p) {
	p=body;
}

QueryWeapon1(p) {
	p=body;
}

AimWeapon1(h,p) {
	return 1;
}

FireWeapon1() {
	var t;
	t=get TARGET_ID(1);
	if(t>0) {
		call-script lua_LaunchDroneWeapon(t, FTORPEDO, torpl1flare, 0, 0);
		call-script lua_LaunchDroneWeapon(t, FTORPEDO, torpr1flare, 0, 0);
		move torpl1 to z-axis [-1.5] speed [10];
		move torpr1 to z-axis [-1.5] speed [10];
		sleep 300;
		move torpl1 to z-axis [0] speed [0.4];
		move torpr1 to z-axis [0] speed [0.4];
		t=get TARGET_ID(1);
		call-script lua_LaunchDroneWeapon(t, FTORPEDO, torpl2flare, 0, 0);
		call-script lua_LaunchDroneWeapon(t, FTORPEDO, torpr2flare, 0, 0);
		move torpl2 to z-axis [-1.5] speed [10];
		move torpr2 to z-axis [-1.5] speed [10];
		sleep 300;
		move torpl2 to z-axis [0] speed [0.4];
		move torpr2 to z-axis [0] speed [0.4];
	}
}