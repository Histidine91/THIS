piece base, body, left, right, enginel, enginer;
piece bodyemit0, bodyemit1, leftemit, rightemit, enginelemit, engineremit, bay0, bay1;
piece turret0, barrel0, gp00, gp01, turret1, barrel1, gp1, turret2, barrel2, gp2;
piece platform0, paturret0, pacannon0, pa0, platform1, paturret1, pacannon1, pa1;
piece tp00, tp01, tp02, tp10, tp11, tp12;
#include "THIS.h"

TRAIL(base,1,1,1)	//fake trail

Create() {
	fireStealthTime = 3000;
	if (!get PERK_MORE_GUNS) {
		hide platform1;
		hide paturret1;
		hide pacannon1;
	}
	if (get PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-2,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-2,KLIGHT_ROF_BOOST);
		get WEAPON_SPRAY(-3,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-3,KLIGHT_ROF_BOOST);
	}
	sleep 30; //to let the MoveCtrl move it
	emit-sfx 1027 from bodyemit0;
	emit-sfx 1027 from bodyemit1;
	turn tp00 to y-axis <-80> now;
	turn tp01 to y-axis <-120> now;
	turn tp02 to y-axis <-150> now;
	turn tp10 to y-axis <80> now;
	turn tp11 to y-axis <120> now;
	turn tp12 to y-axis <150> now;
}

Killed() {
	emit-sfx 1026 from bodyemit0;
	emit-sfx 1026 from bodyemit1;
	emit-sfx 1025 from base;
	spin enginel around y-axis speed <30>;
	spin enginer around x-axis speed <-40>;
	spin left around z-axis speed <20>;
	spin right around z-axis speed <-20>;
	move left to x-axis [-90] speed [19];
	move right to x-axis [90] speed [31];
	move enginer to x-axis [90] speed [14];
	move enginer to z-axis [-90] speed [11];
	move enginel to x-axis [-90] speed [18];
	move enginel to z-axis [-90] speed [8];

	sleep 700;
	hide body;
	hide turret0;
	hide barrel0;

	sleep 900;

	emit-sfx 1026 from leftemit;
	sleep 300;
	hide left;
	hide platform1;
	hide paturret1;
	hide pacannon1;
	
	sleep 600;

	emit-sfx 1024 from rightemit;
	emit-sfx 1024 from platform0;
	emit-sfx 1024 from pacannon0;
	sleep 100;
	hide right;
	hide platform0;
	hide paturret0;
	hide pacannon0;
	
	sleep 300;
	emit-sfx 1026 from engineremit;
	emit-sfx 1024 from engineremit;
	sleep 300;
	hide enginer;
	hide turret1;
	hide barrel1;
	
	sleep 800;
	
	emit-sfx 1025 from enginelemit;
	sleep 700;
	hide enginel;
	hide turret2;
	hide barrel2;
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		show platform1;
		show paturret1;
		show pacannon1;
	}
	else if (p == PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-2,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-2,KLIGHT_ROF_BOOST);
		get WEAPON_SPRAY(-3,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-3,KLIGHT_ROF_BOOST);
	}
}

#define TURRETAIM <160>
#define BARRELAIM <120>
#define MUZZLE (1028 + get PERK_BETTER_KINETICS)

static-var gp;

AimFromWeapon4(p) {
	p=turret0;
}

QueryWeapon4(p) {
	p=gp00 + gp;
}

AimWeapon4(h,p) {
	signal SIG_Aim1;
	set-signal-mask SIG_Aim1;
	turn turret0 to y-axis h speed TURRETAIM;
	turn barrel0 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret0 around y-axis;
	wait-for-turn barrel0 around x-axis;
	return 1;
}

Shot4() {
	gp = !gp;
}



AimFromWeapon2(p) {
	p=turret1;
}

QueryWeapon2(p) {
	p=gp1;
}

AimWeapon2(h,p) {
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn turret1 to y-axis h speed TURRETAIM;
	turn barrel1 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret1 around y-axis;
	wait-for-turn barrel1 around x-axis;
	return 1;
}

Shot2() {
	emit-sfx MUZZLE from gp1;
}



AimFromWeapon3(p) {
	p=turret2;
}

QueryWeapon3(p) {
	p=gp2;
}

AimWeapon3(h,p) {
	signal SIG_Aim3;
	set-signal-mask SIG_Aim3;
	turn turret2 to y-axis h speed TURRETAIM;
	turn barrel2 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret2 around y-axis;
	wait-for-turn barrel2 around x-axis;
	return 1;
}

Shot3() {
	emit-sfx MUZZLE from gp2;
}


#define PLASMAAIM <60>

AimFromWeapon1(p) {
	p=paturret0;
}

QueryWeapon1(p) {
	p=pa0;
}

AimWeapon1(h,p) {
	signal SIG_Aim4;
	set-signal-mask SIG_Aim4;
	turn paturret0 to y-axis h speed PLASMAAIM;
	turn pacannon0 to x-axis 0 - p speed PLASMAAIM;
	wait-for-turn paturret0 around y-axis;
	wait-for-turn pacannon0 around x-axis;
	return 1;
}

Shot1() {
	set STEALTH to 0;
	start-script RestoreStealth();
}


AimFromWeapon5(p) {
	p=paturret1;
}

QueryWeapon5(p) {
	p=pa1;
}

AimWeapon5(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim5;
	set-signal-mask SIG_Aim5;
	turn paturret1 to y-axis h speed PLASMAAIM;
	turn pacannon1 to x-axis 0 - p speed PLASMAAIM;
	wait-for-turn paturret1 around y-axis;
	wait-for-turn pacannon1 around x-axis;
	return 1;
}

Shot5() {
	set STEALTH to 0;
	start-script RestoreStealth();
}


AimFromWeapon6(p) {
	p=base;
}

QueryWeapon6(p) {
	p=base;
}

AimWeapon6() {
	return 1;
}

FireWeapon6() {
	var t;
	t=get TARGET_ID(6);
	if(t>0) {
		call-script lua_LaunchDroneWeapon(t, TORPEDO, tp00, 0);
		call-script lua_LaunchDroneWeapon(t, TORPEDO, tp10, 0);
		//sleep 100;
		call-script lua_LaunchDroneWeapon(get TARGET_ID(6), TORPEDO, tp01, 0);
		call-script lua_LaunchDroneWeapon(get TARGET_ID(6), TORPEDO, tp11, 0);
		//sleep 100;
		call-script lua_LaunchDroneWeapon(get TARGET_ID(6), TORPEDO, tp02, 0);
		call-script lua_LaunchDroneWeapon(get TARGET_ID(6), TORPEDO, tp12, 0);
	}
}

BlockShot6(target, block) {
	block= !target;
}