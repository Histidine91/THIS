piece base, body, left, right;
piece bodyemit, leftemit, rightemit;
piece turret0, barrel0, gp00, gp01, turret1, barrel1, gp10, gp11, turret2, barrel2, gp20, gp21;
piece cannons0, cannons1, cn00, cn01, cn10, cn11;
#include "THIS.h"

static-var gp0, gp1, gp2, cn, cns;

TRAIL(base,1,1,1)	//fake trail

Create() {
	fireStealthTime = 4500;
	if (!get PERK_MORE_GUNS) {
		hide turret2;
		hide barrel2;
	}
	sleep 30; //to let the MoveCtrl move it
	emit-sfx 1027 from leftemit;
	emit-sfx 1027 from rightemit;
	cns= -1;
}

Killed() {
	emit-sfx 1026 from bodyemit;
	emit-sfx 1025 from left;
	sleep 700;
	hide left;
	hide turret1;
	hide barrel1;
	hide turret2;
	hide barrel2;
	hide cannons1;
	move body to x-axis [90] speed [20];
	spin body around z-axis speed <15>;
	sleep 600;
	emit-sfx 1024 from turret0;
	sleep 80;
	emit-sfx 1026 from rightemit;
	sleep 150;
	hide right;
	hide turret0;
	hide barrel0;
	hide cannons0;
	move body to x-axis [90] speed [10];
	sleep 400;
	emit-sfx 1025 from bodyemit;
	sleep 700;
	hide body;
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		show turret2;
		show barrel2;
	}
}

#define TURRETAIM <160>
#define BARRELAIM <120>


AimFromWeapon1(p) {
	p=body;
}

QueryWeapon1(p) {
	p=cn00 + cns;
}

AimWeapon1() {
	return 1;
}

FireWeapon1() {
	if (cn) {
		move cannons1 to z-axis [-15] speed [180];
		sleep 300;
		move cannons1 to z-axis 0 speed [25];
	}
	else {
		move cannons0 to z-axis [-15] speed [180];
		sleep 300;
		move cannons0 to z-axis 0 speed [25];
	}
}

Shot1() {
	cns = cns+1;
	set STEALTH to 0;
	start-script RestoreStealth();
}

EndBurst1() {
	cn = !cn;
	cns = 2*cn -1;
}

AimFromWeapon2(p) {
	p=turret0;
}

QueryWeapon2(p) {
	p=gp00 + gp0;
}

AimWeapon2(h,p) {
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn turret0 to y-axis h speed TURRETAIM;
	turn barrel0 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret0 around y-axis;
	wait-for-turn barrel0 around x-axis;
	return 1;
}

Shot2() {
	gp0 = !gp0;
}


AimFromWeapon3(p) {
	p=turret1;
}

QueryWeapon3(p) {
	p=gp10 + gp1;
}

AimWeapon3(h,p) {
	signal SIG_Aim3;
	set-signal-mask SIG_Aim3;
	turn turret1 to y-axis h speed TURRETAIM;
	turn barrel1 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret1 around y-axis;
	wait-for-turn barrel1 around x-axis;
	return 1;
}

Shot3() {
	gp1 = !gp1;
}


AimFromWeapon4(p) {
	p=turret2;
}

QueryWeapon4(p) {
	p=gp20 + gp2;
}

AimWeapon4(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim4;
	set-signal-mask SIG_Aim4;
	turn turret2 to y-axis h speed TURRETAIM;
	turn barrel2 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret2 around y-axis;
	wait-for-turn barrel2 around x-axis;
	return 1;
}

Shot4() {
	gp2 = !gp2;
}



