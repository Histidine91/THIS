piece body, damage0, damage1, ex;
piece ml0, ml1, ml2, ml3, ml4, ml5;
piece turret0, barrel0, sleeve0, gp0, turret1, sleeve1, barrel1, gp1;

#include "THIS.h"

DamageLoop() {
	signal SIG_Damage;
	set-signal-mask SIG_Damage;
	while((get HEALTH) < 50) {
		set CEG_DAMAGE to 25 - (get HEALTH)/2;
		emit-sfx 1024 from damage0;
		if ((get HEALTH) < 30) emit-sfx 1024 from damage1;
		sleep 50;
	}
}

TRAIL(ex,5,128,16)

static-var ml;
Create() {
	if (!get PERK_MORE_GUNS) {
		hide turret1;
		hide barrel1;
		hide sleeve1;
	}
	if (get PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-2,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-2,KLIGHT_ROF_BOOST);
		get WEAPON_SPRAY(-3,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-3,KLIGHT_ROF_BOOST);
	}
	ml= -1;
	turn ex to y-axis <90> now;
	call-script MoveRate1();
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		show turret1;
		show barrel1;
		show sleeve1;
	}
	else if (p == PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-2,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-2,KLIGHT_ROF_BOOST);
		get WEAPON_SPRAY(-3,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-3,KLIGHT_ROF_BOOST);
	}
}

Killed() {
	emit-sfx 1025 from body;
	sleep 100;
	hide body;
	hide turret0;
	hide barrel0;
	hide sleeve0;
	hide turret1;
	hide barrel1;
	hide sleeve1;
}

HitByWeaponId(z,x,id,d) {
	start-script DamageLoop();
	return 100;
}

#define MUZZLE (1026 + get PERK_BETTER_KINETICS)


AimFromWeapon1(p) {
	p=body;
}

QueryWeapon1(p) {
	p=ml0 + ml;
}

AimWeapon1(h,p) {
	return 1;
}

Shot1() {
	ml=ml+1;
	if (ml > 5) ml = 0;
}

//EndBurst1() {
	//ml= -1;
//}

#define TURRETAIM <100>
#define BARRELAIM <60>

AimFromWeapon2(p) {
	p=turret0;
}

QueryWeapon2(p) {
	p=gp0;
}

AimWeapon2(h,p) {
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn turret0 to y-axis h speed TURRETAIM;
	turn sleeve0 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret0 around y-axis;
	wait-for-turn sleeve0 around x-axis;
	return 1;
}

Shot2() {
	emit-sfx MUZZLE from gp0;
}



AimFromWeapon3(p) {
	p=turret1;
}

QueryWeapon3(p) {
	p=gp1;
}

AimWeapon3(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim3;
	set-signal-mask SIG_Aim3;
	turn turret1 to y-axis h speed TURRETAIM;
	turn sleeve1 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret1 around y-axis;
	wait-for-turn sleeve1 around x-axis;
	return 1;
}

Shot3() {
	emit-sfx MUZZLE from gp1;
}
