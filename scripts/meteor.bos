//linear constant = 65536

piece body, shoulderl, shoulderr, wingl, wingr;
piece mturret, msleeve, mbarrel1, mbarrel2, mflare1, mflare2;
piece missilel, missiler, lpd, rpd;
piece lturret, lsleeve, lbarrel1, lbarrel2, lflare1, lflare2, rturret, rsleeve, rbarrel1, rbarrel2, rflare1, rflare2;
piece exhaustl, exhaustr, exhaustm, damage0, damage1;

#include "THIS.h"

static-var gun1, gun2, gun3, miss1, las1, afterBurnSpeed;

TRAIL(exhaustm,18,128,16)

DamageLoop() {
	signal SIG_Damage;
	set-signal-mask SIG_Damage;
	while((get HEALTH) < 50) {
		set CEG_DAMAGE to 25 - (get HEALTH)/2;
		emit-sfx 1029 from damage0;
		if ((get HEALTH) < 30) emit-sfx 1029 from damage1;
		sleep 50;
	}
}

HitByWeaponId(z,x,id,d) {
	start-script DamageLoop();
	return 100;
}

StartAfterBurn()
{
	set MAX_SPEED to afterBurnSpeed;
	call-script MoveRate1();
}

StopAfterBurn()
{
 	set MAX_SPEED to afterBurnSpeed/2;
 	call-script MoveRate0();
}

Create() {
	gun1 = 0;
	gun2 = 0;
	gun3 = 0;
	miss1 = 0;
	las1 = 0;	
	turn shoulderr to z-axis <-12> now;
	turn shoulderl to z-axis <12> now;
	turn exhaustm to y-axis <90> now;
	if (!get PERK_MORE_GUNS) {
		hide mturret;
		hide msleeve;
		hide mbarrel1;
		hide mbarrel2;
	}
	call-script MoveRate0();
	//afterBurnSpeed = GET MAX_SPEED;
	//set MAX_SPEED to afterBurnSpeed/2;

	sleep 30; //to let the MoveCtrl move it
	emit-sfx 1026 from body;
	if (get PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-5,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-5,KDUAL_ROF_BOOST);
	}
}

Killed() {
	spin body around x-axis speed <10>;
	emit-sfx 1025 from damage0;
	sleep 1200;

	move shoulderl to x-axis [-90] speed [8];
	spin shoulderl around z-axis speed <30>;
	move wingr to x-axis [90] speed [16];
	spin wingr around z-axis speed <-50>;
	emit-sfx 1025 from damage1;
	sleep 1200;
	emit-sfx 1024 from shoulderl;
	hide shoulderl;
	emit-sfx 1024 from body;
	sleep 1400;
	emit-sfx 1025 from body;
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		show mturret;
		show msleeve;
		show mbarrel1;
		show mbarrel2;
	}
	else if (p == PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-5,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-5,KDUAL_ROF_BOOST);
	}
	
}

#define LTURRETAIM <180>
#define LBARRELAIM <90>

AimFromWeapon1(p) {
	p=body;
}

QueryWeapon1(p) {
	if (miss1) {p = missilel;}
	else {p = missiler;}
}

AimWeapon1(h,p) {
	return 1;
}

Shot1() {
	miss1 = !miss1;
}

AimFromWeapon2(p) {
	p=lturret;
}

QueryWeapon2(p) {
	if (gun1) {p = lbarrel1;}
	else {p = lbarrel2;}
}

AimWeapon2(h,p) {
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn lturret to y-axis h speed LTURRETAIM;
	turn lsleeve to x-axis 0 - p speed LBARRELAIM;
	wait-for-turn lturret around y-axis;
	wait-for-turn lsleeve around x-axis;
	return 1;
}

Shot2() {
	gun1 = !gun1;
}

AimFromWeapon3(p) {
	p=rturret;
}

QueryWeapon3(p) {
	if (gun1) {p = rbarrel1;}
	else {p = rbarrel2;}
}

AimWeapon3(h,p) {
	signal SIG_Aim3;
	set-signal-mask SIG_Aim3;
	turn rturret to y-axis h speed LTURRETAIM;
	turn rsleeve to x-axis 0 - p speed LBARRELAIM;
	wait-for-turn rturret around y-axis;
	wait-for-turn rsleeve around x-axis;
	return 1;
}

Shot3() {
	gun2 = !gun2;
}

AimFromWeapon4(p) {
	p=body;
}

QueryWeapon4(p) {
	if (las1) {p = lpd;}
	else {p = rpd;}
}

AimWeapon4(h,p) {
	return 1;
	//return 0;
}

Shot4() {
	las1 = !las1;
}

#define MUZZLE (1027 + get PERK_BETTER_KINETICS)

QueryWeapon5(p) {
	if (gun1) {p = mbarrel1;}
	else {p = mbarrel2;}
}

AimWeapon5(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim6;
	set-signal-mask SIG_Aim6;
	turn mturret to y-axis h speed LTURRETAIM;
	turn msleeve to x-axis 0 - p speed LBARRELAIM;
	wait-for-turn mturret around y-axis;
	wait-for-turn msleeve around x-axis;
	return 1;
}

Shot5() {
	if(gun3)
	{ emit-sfx MUZZLE from mflare1;}
	else { emit-sfx MUZZLE from mflare2;}
	gun3 = !gun3;
}

EMPBomb() {
	emit-sfx 4101 from body;
}


