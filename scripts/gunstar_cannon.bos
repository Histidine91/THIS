piece body, fin, arm, shield, damage0, damage1;
piece laserpod, laserbarrel, laserflare;
piece axis, sleeve1, barrel1, flare1, sleeve2, barrel2, flare2;
#include "THIS.h"

static-var gun1;

DamageLoop() {
	signal SIG_Damage;
	set-signal-mask SIG_Damage;
	while((get HEALTH) < 50) {
		set CEG_DAMAGE to 25 - (get HEALTH)/2;
		emit-sfx 1028 from damage0;
		if ((get HEALTH) < 30) emit-sfx 1028 from damage1;
		sleep 50;
	}
}

HitByWeaponId(z,x,id,d) {
	start-script DamageLoop();
	return 100;
}

Create() {
	if (!get PERK_MORE_GUNS) {
		hide laserpod;
		hide laserbarrel;
	}
	if (get PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-1,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-1,KDUAL_ROF_BOOST);
	}
	gun1 = 0;
	permaStealth = 1;
	
	sleep 30; //to let the MoveCtrl move it
	//emit-sfx 1025 from body;
}

Killed() {
	emit-sfx 1024 from body;
	//get PLAY_SOUND(S_DEATH_SMALL,[5]);
	sleep 100;
	hide body;
	hide arm;
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		show laserpod;
		show laserbarrel;
	}
	else if (p == PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-1,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-1,KDUAL_ROF_BOOST);
	}
}

#define TURRETAIM <160>
#define BARRELAIM <120>

#define MUZZLE (1026 + get PERK_BETTER_KINETICS)


AimFromWeapon1(p) {
	p=axis;
}

QueryWeapon1(p) {
	if (gun1) {p=flare1;}
	else {p=flare2;}
}

AimWeapon1(h,p) {
	signal SIG_Aim1;
	set-signal-mask SIG_Aim1;
	turn axis to x-axis h speed (TURRETAIM/2);
	turn arm to y-axis p speed (BARRELAIM/2);
	return 1;
}

Shot1() {
	if(gun1)
	{ emit-sfx MUZZLE from flare1;}
	else { emit-sfx MUZZLE from flare2;}
	gun1 = !gun1;
}

AimFromWeapon2(p) {
	p=laserpod;
}

QueryWeapon2(p) {
	p=laserflare;
}

AimWeapon2(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn laserbarrel to x-axis h speed TURRETAIM;
	turn laserbarrel to y-axis 0 - p speed BARRELAIM;
	return 1;
}