//linear constant = 65536

piece body, lwing, rwing, Lengine, Rengine;
piece firep1, firep2;
piece damage0, damage1, bay0, bay1;

#include "THIS.h"

static-var miss1, dock;

TRAIL(body,1,1,1)	//fake trail

DamageLoop() {
	signal SIG_Damage;
	set-signal-mask SIG_Damage;
	while((get HEALTH) < 50) {
		set CEG_DAMAGE to 25 - (get HEALTH)/2;
		emit-sfx 1027 from damage0;
		if ((get HEALTH) < 30) emit-sfx 1027 from damage1;
		sleep 50;
	}
}

HitByWeaponId(z,x,id,d) {
	start-script DamageLoop();
	return 100;
}


QueryTransport(piecenum){
	dock = !dock;
	if (dock == 1) piecenum = bay0;
	else piecenum = bay1;
}

Create() {
	miss1 = 0;
	dock = 1;
	turn bay0 to y-axis <90> now;
	turn bay1 to y-axis <-90> now;
	//afterBurnSpeed = GET MAX_SPEED;
	//set MAX_SPEED to afterBurnSpeed/2;

	sleep 30; //to let the MoveCtrl move it
	emit-sfx 1026 from body;
}

Killed() {
	spin body around x-axis speed <20>;
	emit-sfx 1025 from damage0;
	sleep 1200;

	move lwing to x-axis [-90] speed [8];
	spin lwing around z-axis speed <30>;
	move rwing to x-axis [90] speed [16];
	spin rwing around z-axis speed <-50>;
	emit-sfx 1025 from damage1;
	sleep 1200;
	emit-sfx 1024 from body;
	sleep 1400;
}

AimFromWeapon1(p) {
	p=body;
}

QueryWeapon1(p) {
	if (miss1) {p = firep1;}
	else {p = firep2;}
}

AimWeapon1(h,p) {
	return 1;
}

Shot1() {
	miss1 = !miss1;
}
