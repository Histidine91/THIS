piece base, body, drone0, drone1, d0, d1, damage0, damage1, ex, turret0, barrel0, gp0;

#include "THIS.h"

DamageLoop() {
	signal SIG_Damage;
	set-signal-mask SIG_Damage;
	while((get HEALTH) < 50) {
		set CEG_DAMAGE to 25 - (get HEALTH)/2;
		emit-sfx 1024 from damage0;
		if ((get HEALTH) < 30) emit-sfx 1024 from damage1;
		sleep 50;
	}
}

TRAIL(ex,7,64,2)

Create() {
	if (!get PERK_MORE_GUNS) {
		hide turret0;
		hide barrel0;
	}
	if (get PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-2,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-2,KLIGHT_ROF_BOOST);
	}
	sleep 1;
	turn ex to y-axis <90> now;
	turn ex to x-axis <90> now;
	start-script MoveRate1();
}

Killed() {
	emit-sfx 1025 from base;
	hide drone0;
	hide drone1;
	hide body;
}

NewPerk(p) {
	if (p == PERK_MORE_GUNS) {
		show turret0;
		show barrel0;
	}
	else if (p == PERK_BETTER_KINETICS) {
		get WEAPON_SPRAY(-2,KLIGHT_SPRAY_BOOST);
		get WEAPON_RELOADTIME(-2,KLIGHT_ROF_BOOST);
	}
}



AimWeapon1() {
	return 1;
}

RearmDrones() {
	sleep 10000;
	turn drone0 to y-axis 0 now;
	show drone0;
	turn drone1 to y-axis 0 now;
	show drone1;
}

BlockShot1(target,block) {
	block = target <= 0;
}

FireWeapon1() {
	turn drone0 to y-axis <-20> speed <40>;
	turn drone1 to y-axis <20> speed <40>;
	sleep 500;
	call-script lua_LaunchDroneWeapon(get TARGET_ID(1), DRONE_K, d0, -1);
	hide drone0;
	sleep 500;
	call-script lua_LaunchDroneWeapon(get TARGET_ID(1), DRONE_K, d1, 1);
	hide drone1;
	start-script RearmDrones();
}


#define TURRETAIM <100>
#define BARRELAIM <60>
#define MUZZLE (1026 + get PERK_BETTER_KINETICS)

AimFromWeapon2(p) {
	p=turret0;
}

QueryWeapon2(p) {
	p=gp0;
}

AimWeapon2(h,p) {
	if (!get PERK_MORE_GUNS) return 0;
	signal SIG_Aim2;
	set-signal-mask SIG_Aim2;
	turn turret0 to y-axis h speed TURRETAIM;
	turn barrel0 to x-axis 0 - p speed BARRELAIM;
	wait-for-turn turret0 around y-axis;
	wait-for-turn barrel0 around x-axis;
	return 1;
}

Shot2() {
	emit-sfx MUZZLE from gp0;
}
